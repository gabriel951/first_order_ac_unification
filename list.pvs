% Authors:
%		Gabriel Ferreira Silva (*)
% 		Mauricio Ayala-Rincón (*)
% 		Daniele Nantes Sobrinho (*)
% 		Maribel Fernandez (**)
%		(*) - University of Brasília			(**) - King's College London

% File: list.pvs
% Description: Properties about lists and maps
%%%%%%%%%%%%%%%%%%%%%%%

list_nat_theory: THEORY
BEGIN 

n, i, j, min, max, inc: VAR nat

lst_nat, lst_nat1, lst_nat2: VAR list[nat]

% the lemmas of this theory are proved together with list_theory
dot_product(lst_nat1, lst_nat2): RECURSIVE nat =
    IF null?(lst_nat1) OR null?(lst_nat2) THEN 0
    ELSE car(lst_nat1) * car(lst_nat2) + dot_product(cdr(lst_nat1), cdr(lst_nat2))
    ENDIF
MEASURE lst_nat1 BY << 

sum_lsts(lst_nat1, lst_nat2): RECURSIVE list[nat] =
    IF null?(lst_nat1) OR null?(lst_nat2) THEN null
    ELSE cons(car(lst_nat1) + car(lst_nat2), sum_lsts(cdr(lst_nat1), cdr(lst_nat2)))
    ENDIF
MEASURE lst_nat1 BY << 

END list_nat_theory

list_theory[T: TYPE]: THEORY
BEGIN
     IMPORTING list_nat_theory

x, y, z: VAR T
pair: VAR [T, T]

pair_lst: VAR [list[T], list[T]]
lst_pair, lst_pair1, lst_pair2: VAR list[[T, T]]

lst, lst1, lst2, lst3, new_lst1, new_lst2: VAR list[T]
lst_of_lsts, lst_of_lsts1, lst_of_lsts2, matrix, matrix1, matrix2: VAR list[list[T]]
fin_set, fin_set1, fin_set2: VAR finite_set[T]
lst_of_fin_sets: VAR list[finite_set[T]]


fun: VAR [T -> T]
f: VAR [[T, T] -> T]
g: VAR [T -> list[T]]
h: VAR [list[T] -> T]

num: VAR number
n, i, j, min, max, inc: VAR nat
coef, coef1, coef2, lst_nat, lst_nat1, lst_nat2: VAR list[nat]
lst_pos_nat: VAR list[posnat]

% an equivalence definition
equiv: TYPE = {R: PRED[[T, T]]| equivalence?(R)}
equal?: VAR equiv

from_min2max(min, max): RECURSIVE list[nat] =
    IF min > max THEN null
    ELSIF min = max THEN cons(min, null) 
    ELSE cons(min, from_min2max(min + 1, max)) 
    ENDIF
MEASURE max(0, max-min) 

% increments by "inc" the number in position "n" of the list of natural numbers "lst_nat"
% we start counting at 0
increment(lst_nat, n, inc): RECURSIVE list[nat] =
    IF null?(lst_nat) THEN lst_nat
    ELSE LET x = car(lst_nat) IN
         IF n = 0 THEN cons(x + inc, cdr(lst_nat))
	 ELSE cons(x, increment(cdr(lst_nat), n - 1, inc))
	 ENDIF
    ENDIF
MEASURE lst_nat BY << 


% receives lst1 = [x1, ..., xn] and lst2 = [y1, ..., yn]
% returns [f(x1, y1), ..., f(xn, yn)]
apply(lst1, lst2, f): RECURSIVE list[T] =
    IF null?(lst1) OR null?(lst2) THEN null
    ELSE LET x = car(lst1), y = car(lst2) IN
         cons(f(x, y), apply(cdr(lst1), cdr(lst2), f))
    ENDIF
MEASURE lst1 BY << 

% append_pair((l1, l2)) = append(l1, l2)
append_pair(pair_lst): list[T] =
    LET lst1 = pair_lst`1,
    	lst2 = pair_lst`2
	IN append(lst1, lst2)

% for every y in lst2, obtain the pair (x, y)
cartesian_product_aux(x, lst2): RECURSIVE list[[T, T]] =
    IF null?(lst2) THEN null
    ELSE LET y = car(lst2) IN
    	     cons((x, y), cartesian_product_aux(x, cdr(lst2)))
    ENDIF
MEASURE lst2 BY << 

% cartesian product of two lists
cartesian_product(lst1, lst2): RECURSIVE list[[T, T]] =
    IF null?(lst1) THEN null
    ELSE LET x = car(lst1)
    	 IN  append(cartesian_product_aux(x, lst2), cartesian_product(cdr(lst1), lst2))
    ENDIF 
MEASURE lst1 BY <<  


finset2list(fin_set): RECURSIVE list[T] = 
   IF empty?(fin_set) THEN null
   ELSE cons(choose(fin_set), finset2list(rest(fin_set)))
   ENDIF
MEASURE card(fin_set)

% receives a finite set and returns the 
% receives a list of lists of elements of type T. Outputs a list containing all these elements of type T
% For example, if it receives [[1], [2,3], [4]] it outputs [1, 2, 3, 4]
flatten(lst_of_lsts): RECURSIVE list[T] =
    IF null?(lst_of_lsts) THEN null
    ELSE LET lst = car(lst_of_lsts)
    IN append(lst, flatten(cdr(lst_of_lsts)))
    ENDIF
MEASURE lst_of_lsts BY <<

% let lst_i = nth(lst_of_lsts, i) and x = nth(lst_i, j).
% returns 0 when i or j are not valid indexes
% find the index "n" such that x = nth(flatten(lst_of_lsts), n)
ind_flatten(lst_of_lsts, i, j): RECURSIVE nat =
    IF i >= length(lst_of_lsts) THEN 0
    ELSE LET lst = car(lst_of_lsts) IN
       IF i = 0 THEN
          IF j >= length(lst) THEN 0
          ELSE j 
	  ENDIF
       ELSE length(lst) + ind_flatten(cdr(lst_of_lsts), i-1, j) 
       ENDIF
    ENDIF
MEASURE lst_of_lsts BY <<
	 
no_null_entries?(lst_of_lsts): RECURSIVE bool =
    IF null?(lst_of_lsts) THEN TRUE
    ELSIF null?(car(lst_of_lsts)) THEN FALSE
    ELSE no_null_entries?(cdr(lst_of_lsts)) 
    ENDIF
MEASURE lst_of_lsts BY << 

% returns a bool to indicate if x is in lst and a natural number, to indicate the position
% we start counting the position at 0
pos?(x, lst, equal?): RECURSIVE [bool, nat] =
    IF null?(lst) THEN (FALSE, 0)
    ELSE LET y = car(lst) IN
    	 IF equal?(x, y) THEN (TRUE, 0)
	 ELSE LET (bool, n) = pos?(x, cdr(lst), equal?) IN (bool, n+1)
	 ENDIF
    ENDIF
MEASURE lst BY << 

% the first call to this function should be count_elem(null, null, lst2)
% lst2, is a list of elements [l1, ..., ln] possibly with repetitions
% lst1, when the function finish, contains all elements initially in lst2, without repetitions
% coef contain the number of repetitions of the elements in lst2
count_elem(lst1, coef, lst2, equal?): RECURSIVE [list[T], list[nat]] =
    IF null?(lst2) THEN (lst1, coef) 
    ELSE LET
    	 x = car(lst2),
    	 (in_lst, n) = pos?(x, lst1, equal?)
	 IN IF in_lst THEN LET
	     	new_coef = increment(coef, n, 1)
	     	IN count_elem(lst1, new_coef, cdr(lst2), equal?)
	    ELSE LET
	     	new_lst1 = append(lst1, cons(x, null)),
	     	new_coef = append(coef, cons[nat](1, null)) 
	     	IN count_elem(new_lst1, new_coef, cdr(lst2), equal?)
	    ENDIF
    ENDIF
MEASURE lst2 BY << 

% receives a lst_of_lsts = [l1, ..., ln] and an element x
% put element x as the first element of each lst li.
% the result is [cons(x, l1), ..., cons(x, ln)]
put_in_head(lst_of_lsts, x): RECURSIVE list[list[T]] =
    IF null?(lst_of_lsts) THEN null
    ELSE LET lst = car(lst_of_lsts) IN
	     cons(cons(x, lst), put_in_head(cdr(lst_of_lsts), x))
    ENDIF
MEASURE lst_of_lsts BY <<

% receives lst1 = [x1, ..., xm] and lst2 = [y1, ..., yn]
% returns a list with m entries, putting every element of lst1 as
% the head and lst2 as the tail:
% [[x1, y1, ..., yn], ..., [xm, y1, ..., yn]]
put_in_head(lst1, lst2): RECURSIVE list[list[T]] = 
   IF null?(lst1) THEN null
   ELSE LET x = car(lst1) IN
    	cons(cons(x, lst2), put_in_head(cdr(lst1), lst2))
   ENDIF
MEASURE lst1 BY << 

% returns a list [x, ..., x] of length n
get_repeat_lst(x, n): RECURSIVE list[T] =
    IF n = 0 THEN null
    ELSE cons(x, get_repeat_lst(x, n-1))
    ENDIF
MEASURE n

% selects the greatest element in lst_nat
select_greater(lst_nat): RECURSIVE nat =
    IF null?(lst_nat) THEN 0
    ELSE max(car(lst_nat), select_greater(cdr(lst_nat)))
    ENDIF
MEASURE lst_nat BY << 

% returns TRUE if lst_of_lsts is a matrix with all rows having n columns
matrix?(lst_of_lsts, n): RECURSIVE bool = 
    IF null?(lst_of_lsts) THEN TRUE
    ELSE IF length(car(lst_of_lsts)) = n THEN matrix?(cdr(lst_of_lsts), n)
    	 ELSE FALSE
	 ENDIF
    ENDIF
MEASURE lst_of_lsts BY << 

% returns TRUE if lst_of_lsts is a matrix. Returns FALSE otherwise
matrix?(lst_of_lsts): bool =
    IF null?(lst_of_lsts) THEN TRUE
    ELSE LET lst = car(lst_of_lsts) IN
    	     matrix?(lst_of_lsts, length(lst))
    ENDIF

% returns TRUE if lst_of_lsts is not a matrix. Returns FALSE otherwise
nonmatrix?(lst_of_lsts): bool = NOT matrix?(lst_of_lsts)

% receives a list lst = [x1, ..., xn]
% returns a list [lst_1, ..., lst_m] such that every lst_i is a sublist of lst
get_all_sublists(lst): RECURSIVE list[list[T]] =
    IF null?(lst) THEN null
    ELSIF length(lst) = 1 THEN cons(lst, null)
    ELSE LET x = car(lst),
	     % get all lists without the element x
	     lst_of_lsts1 = get_all_sublists(cdr(lst)), 
	     % get all lists with the element x
	     lst_of_lsts2 = put_in_head(lst_of_lsts1, x)
	     IN append(lst_of_lsts1, lst_of_lsts2)
    ENDIF
MEASURE lst BY <<

% count how many times the element x appears on lst
% TODO: I think this function is deprecated...
count(x, lst): RECURSIVE nat =
    IF null?(lst) THEN 0
    ELSE LET y = car(lst) IN
    	 IF x = y THEN 1 + count(x, cdr(lst))
	 ELSE count(x, cdr(lst))
	 ENDIF
    ENDIF
MEASURE lst BY <<

% count how many times an element equal to x appears on lst
count(x, lst, equal?): RECURSIVE nat =
    IF null?(lst) THEN 0
    ELSE LET y = car(lst) IN
    	 IF equal?(x, y) THEN 1 + count(x, cdr(lst), equal?)
	 ELSE count(x, cdr(lst), equal?)
	 ENDIF
    ENDIF
MEASURE lst BY <<

% curried version of function count
count(x, equal?)(lst): nat = count(x, lst, equal?)

% let lst_of_lsts = [l1, ..., ln].
% get the ith element (we start counting at 1) in l1 "x1_i", ..., get the ith element in ln "xn_i"
% returns [x1_i, ..., xn_i]
% if a list lj has no ith element, do not put anything
get_col_i(lst_of_lsts, i): RECURSIVE list[T] =
    IF null?(lst_of_lsts) THEN null
    ELSE LET lst = car(lst_of_lsts) IN
    	 IF i <= length(lst) AND i > 0 THEN LET y = nth(lst, i-1) IN
	    cons(y, get_col_i(cdr(lst_of_lsts), i))
	 ELSE get_col_i(cdr(lst_of_lsts), i)
	 ENDIF
    ENDIF
MEASURE lst_of_lsts BY << 

% let lst1 = [x1, ..., xn] and lst2 = [y1, ..., yn]
% pareate returns [(x1, y1), ..., (xn, yn)]
pareate(lst1, lst2): RECURSIVE list[[T, T]] =
    IF null?(lst1) OR null?(lst2) THEN null
    ELSE LET x = car(lst1), y = car(lst2) IN cons((x, y), pareate(cdr(lst1), cdr(lst2)))
    ENDIF
MEASURE lst1 BY <<

% receives lst = [x1, ..., xn, xn+1, ..., xj] and an index n
% returns lst1 = [x1, ..., xn]
get_lst_until_n(lst, n): RECURSIVE list[T] =
    IF n = 0 OR null?(lst) THEN null
    ELSE LET x = car(lst) IN
    	     cons(x, get_lst_until_n(cdr(lst), n-1))
    ENDIF
MEASURE n 

% receives lst = [x1, ..., xn, xn+1, ..., xj] and an index n
% returns lst2 = [xn+1, ..., xj]
get_lst_after_n(lst, n): RECURSIVE list[T] =
    IF n = 0 OR null?(lst) THEN lst
    ELSE get_lst_after_n(cdr(lst), n-1)
    ENDIF
MEASURE n 

% split lst = [x1, ..., xn, xn+1, ..., xj] into two lists:
% lst1 = [x1, ..., xn] and lst2 = [xn+1, ..., xj]
split(lst, n): [list[T], list[T]] =
    (get_lst_until_n(lst, n), get_lst_after_n(lst, n))  

split_matrix(matrix, n): RECURSIVE [list[list[T]], list[list[T]]] =
    IF null?(matrix) THEN (null, null)
    ELSE LET
    	 row = car(matrix),
	 (row1, row2) = split(row, n), 
	 (cdr_matrix1, cdr_matrix2) = split_matrix(cdr(matrix), n) IN
	 (cons(row1, cdr_matrix1), cons(row2, cdr_matrix2))
    ENDIF
MEASURE matrix BY << 
    



% receives a list of the form [[x1, y1], ..., [xn, yn]]
% returns [y1, ..., yn]
snd_proj(lst_pair): RECURSIVE list[T] = 
    IF null?(lst_pair) THEN null
    ELSE LET y = car(lst_pair)`2 IN
         cons(y, snd_proj(cdr(lst_pair)))
    ENDIF
MEASURE lst_pair BY << 

% receives a list[x1, ..., xn] and a list of multiplicities[a1, ..., an]
% returns a list with a1 occurrences of x1, ..., an occurrences of xn
get_lst_mult(lst, coef): RECURSIVE list[T] =
    IF null?(lst) OR null?(coef) THEN null 
    ELSE LET x = car(lst), n = car(coef), new_lst = get_repeat_lst(x, n) IN
    	     append(new_lst, get_lst_mult(cdr(lst), cdr(coef))) 
    ENDIF
MEASURE lst BY << 


% remove the ith element from lst.
% counting starts at 0
remove(lst, i): RECURSIVE list[T] =
    IF null?(lst) THEN null 
    ELSIF i = 0 THEN cdr(lst) 
    ELSE LET x = car(lst) IN cons(x, remove(cdr(lst), i-1))
    ENDIF
MEASURE lst BY << 

% receives lst_of_lsts = [lst_0, ..., lst_i, ..., lst_n]
% removes the jth element of lst_i, obtaining lst'_i
% returns lst_of_lsts = [lst_0, ..., lst'_i, ..., lst_n]
% counting start at 0 
remove(lst_of_lsts, i, j): RECURSIVE list[list[T]] =
    IF null?(lst_of_lsts) THEN null 
    ELSE LET lst = car(lst_of_lsts) IN
         IF i = 0 THEN
	     IF length(lst) <= 1 THEN cdr(lst_of_lsts)
	     ELSE cons(remove(lst, j), cdr(lst_of_lsts))
	     ENDIF
	 ELSE cons(lst, remove(cdr(lst_of_lsts), i-1, j))
	 ENDIF
    ENDIF
MEASURE lst_of_lsts BY << 

% recursively removes the elements of lst1 if it appears on lst2
% when an element of lst1 is removed, one occurrence of it in lst2 is also removed
remove_common(lst1, lst2, equal?): RECURSIVE list[T] =
    IF null?(lst1) THEN null
    ELSE LET x = car(lst1),
         (common, i) = pos?(x, lst2, equal?) IN
	 IF common THEN remove_common(cdr(lst1), remove(lst2, i), equal?)
	 ELSE cons(x, remove_common(cdr(lst1), lst2, equal?))
	 ENDIF
    ENDIF
MEASURE lst1 BY << 

% get the elements of lst1 that also appear in lst2
% if an element "x" happens "m" times in lst1 and "n" times in lst2 then
% he will appear "min(m, n)" times in get_common(lst1, lst2, equal?)
get_common(lst1, lst2, equal?): RECURSIVE list[T] = 
    IF null?(lst1) THEN null
    ELSE LET x = car(lst1),
         (common, i) = pos?(x, lst2, equal?) IN
	 IF common THEN cons(x, get_common(cdr(lst1), remove(lst2, i), equal?))
	 ELSE get_common(cdr(lst1), lst2, equal?)
	 ENDIF
    ENDIF
MEASURE lst1 BY << 

same_count?(lst1, lst2, equal?): bool =
    FORALL x: count(x, lst1, equal?) = count(x, lst2, equal?)

sublist?(lst1, lst2): bool =
    EXISTS lst2, lst3: lst2 = append(lst2, append(lst1, lst3))


%%%%%%%%%%%%%%%%%%%%% Theorems
%% general properties
member_append: LEMMA
    member(x, append(lst1, lst2)) = (member(x, lst1) OR member(x, lst2)) 

member_append_nat: LEMMA
    member(n, append(lst_nat1, lst_nat2)) = (member(n, lst_nat1) OR member(n, lst_nat2)) 

member_append2: LEMMA
    member((x, y), append(lst_pair1, lst_pair2)) = (member((x,y), lst_pair1) OR member((x,y), lst_pair2))

null_append: LEMMA
    null?(append(lst1, lst2)) = (null?(lst1) AND null?(lst2))

null_append_nat: LEMMA
    null?(append(lst_nat1, lst_nat2)) = (null?(lst_nat1) AND null?(lst_nat2))

cdr_append: LEMMA
    NOT null?(lst1) IMPLIES cdr(append(lst1, lst2)) = append(cdr(lst1), lst2)

equal_append_implies_null: LEMMA
    lst1 = append(lst2, lst1) IMPLIES null?(lst2)

equal_append_implies_equal: LEMMA
    append(lst1, lst2) = append(lst3, lst2) IMPLIES lst1 = lst3

lst2set_append_mem: LEMMA
    member(x, list2set(append(lst1, lst2))) IFF (member(x, list2set(lst1)) OR member(x, list2set(lst2)))

lst2set_is_finite: LEMMA
    is_finite(list2set(lst1))
    
nth_append: LEMMA
    i < length(lst1) + length(lst2) IMPLIES
    nth(append(lst1, lst2), i) =
    IF i < length(lst1) THEN nth(lst1, i)
    ELSE nth(lst2, i - length(lst1))
    ENDIF

equal_lst_nth: LEMMA 
    length(lst1) = length(lst2) AND (FORALL (n: below[length(lst1)]): nth(lst1, n) = nth(lst2, n))
    IMPLIES lst1 = lst2

% a property about nth and member
member_nth: LEMMA
    i < length(lst) IMPLIES member(nth(lst, i), lst)

member_nth2: LEMMA
    member(x, lst) = EXISTS (n:below[length(lst)]): x = nth(lst, n)

% dot product
dot_product_sym: LEMMA
    dot_product(lst_nat1, lst_nat2) = dot_product(lst_nat2, lst_nat1)

dot_product_sum_lsts: LEMMA
    length(lst_nat1) = length(lst_nat2) IMPLIES 
    dot_product(sum_lsts(lst_nat1, lst_nat2), coef) =
    dot_product(lst_nat1, coef) + dot_product(lst_nat2, coef)

dot_product_zero_lst: LEMMA
    (FORALL n: member(n, lst_nat1) IMPLIES n = 0)
    IMPLIES dot_product(lst_nat1, lst_nat2) = 0

% cartesian product
cart_prod_aux_mem: LEMMA
    member(y, lst) = member((x, y), cartesian_product_aux(x, lst))

cart_prod_aux_mem2: LEMMA
    member((x, y), cartesian_product_aux(z, lst)) => x = z
    
cart_prod_mem: LEMMA
    (member(x, lst1) AND member(y, lst2)) = member((x, y), cartesian_product(lst1, lst2))
    
% finset2list
finset2list_mem: LEMMA
    member(x, finset2list(fin_set)) IFF member(x, fin_set)

% flatten
null_flatten_nth: LEMMA
    i < length(lst_of_lsts) AND null?(flatten(lst_of_lsts)) IMPLIES
    null?(nth(lst_of_lsts, i))

flatten_append: LEMMA
    flatten(append(lst_of_lsts1, lst_of_lsts2)) = append(flatten(lst_of_lsts1), flatten(lst_of_lsts2))

len0_ind_flatten: LEMMA
    length(flatten(lst_of_lsts)) = 0 IMPLIES ind_flatten(lst_of_lsts, i, j) = 0

ind_flatten_len: LEMMA
    i < length(lst_of_lsts) AND j < length(nth(lst_of_lsts, i)) IMPLIES
    ind_flatten(lst_of_lsts, i, j) < length(flatten(lst_of_lsts))

nth_flatten: LEMMA 
    i < length(lst_of_lsts) AND j < length(nth(lst_of_lsts, i)) IMPLIES
    nth(nth(lst_of_lsts, i), j) = nth(flatten(lst_of_lsts), ind_flatten(lst_of_lsts, i, j))

flatten_mem: LEMMA
    member(x, flatten(lst_of_lsts)) IMPLIES
    EXISTS (n: below[length(lst_of_lsts)]):
        member(x, nth(lst_of_lsts, n))

% no_null_entries?
no_null_entries?_nth: LEMMA
    (FORALL n: n < length(lst_of_lsts) IMPLIES NOT null?(nth(lst_of_lsts, n)))
    IMPLIES no_null_entries?(lst_of_lsts)

% from_min2max:
from_min2max_len: LEMMA
    length(from_min2max(i, n)) = max(0, n-i+1)

from_min2max_len1: LEMMA
    length(from_min2max(1, n)) = n

from_min2max_car: LEMMA
    NOT null?(from_min2max(i, j)) IMPLIES car(from_min2max(i, j)) = i

nth_from_min2max: LEMMA
    n < length(from_min2max(i, j)) IMPLIES
    nth(from_min2max(i, j), n) = i + n

nth_from_min2max2: LEMMA
    n < length(from_min2max(1, j)) IMPLIES
    nth(from_min2max(1, j), n) = n + 1
    
from_min2max_mem: LEMMA
   i <= j IMPLIES (member(n, from_min2max(i, j)) IFF (i <= n AND n <= j))

% increment, pos?
increment_len: LEMMA
    length(increment(lst_nat, n, inc)) = length(lst_nat)

increment_mantains_gt0: LEMMA
    (FORALL i: member(i, coef) IMPLIES i > 0) IMPLIES
    (FORALL i: member(i, increment(coef, n, 1)) IMPLIES i > 0)

pos?_len: LEMMA
   pos?(x, lst, equal?)`1 IMPLIES
   pos?(x, lst, equal?)`2 < length(lst)

pos?_cor: LEMMA
   pos?(x, lst, equal?)`1 IMPLIES equal?(x, nth(lst, pos?(x, lst, equal?)`2))

pos?_cor2: LEMMA
   pos?(x, lst, equal?)`1 AND pos?(y, lst, equal?)`1 AND
   pos?(x, lst, equal?)`2 = pos?(y, lst, equal?)`2 IMPLIES
   equal?(x, y)
   
pos?_equal: LEMMA
   equal?(x, y) AND pos?(x, lst, equal?)`1 IMPLIES pos?(y, lst, equal?)`1

pos?_equal2: LEMMA
   equal?(x, y) IMPLIES pos?(x, lst, equal?)`2 = pos?(y, lst, equal?)`2

pos?_remove: LEMMA
   pos?(x, remove(lst, n), equal?)`1 IMPLIES pos?(x, lst, equal?)`1

pos?_remove_nth: LEMMA
   n < length(lst) IMPLIES 
   pos?(x, lst, equal?)`1 = (pos?(x, remove(lst, n), equal?)`1 OR equal?(x, nth(lst, n)))

pos?_remove_ind_lt: LEMMA
    LET (bool, ind) = pos?(x, lst, equal?) IN
    bool and ind < n IMPLIES
    pos?(x, remove(lst, n), equal?)`2 = ind
    
pos?_remove_ind_gt: LEMMA
    LET (bool, ind) = pos?(x, lst, equal?) IN
    bool and ind > n IMPLIES
    pos?(x, remove(lst, n), equal?)`2 = ind - 1

% count_elem
count_elem_len: LEMMA
    LET (new_lst, new_coef) = count_elem(lst, coef, lst2, equal?) IN
    length(lst) = length(coef) IMPLIES
    length(new_lst) = length(new_coef)

count_elem_null1: LEMMA
    null?(count_elem(lst, coef, lst2, equal?)`1) IMPLIES null?(lst2)

count_elem_null2: LEMMA
    length(lst) = length(coef) AND
    null?(count_elem(lst, coef, lst2, equal?)`2) IMPLIES null?(lst2) 

count_elem_gt0: LEMMA
    (FORALL i: member(i, coef) IMPLIES i > 0) AND member(n, count_elem(lst, coef, lst2, equal?)`2)
    IMPLIES n > 0 

count_elem_mem: LEMMA
    member(x, count_elem(lst, coef, lst2, equal?)`1) IMPLIES member(x, lst) OR member(x, lst2) 

% put in head
put_in_head_matrix?: LEMMA
    matrix?(matrix, n) IMPLIES matrix?(put_in_head(matrix, x), n+1)

put_in_head_mem: LEMMA
    member(lst, put_in_head(matrix, x)) IMPLIES
    (NOT null?(lst)) AND x = car(lst) AND member(cdr(lst), matrix)

put_in_head_mem2: LEMMA
    member(lst, put_in_head(lst1, lst2)) IMPLIES
    null?(lst) OR (member(car(lst), lst1) AND cdr(lst) = lst2)

put_in_head_mem3: LEMMA
    member(x, lst) AND member(lst, put_in_head(lst1, lst2)) IMPLIES
    member(x, lst1) OR member(x, lst2)

% get_repeat_lst
get_repeat_lst_len: LEMMA
    length(get_repeat_lst(x, n)) = n

get_repeat_lst_append: LEMMA
    append(get_repeat_lst(x, i), get_repeat_lst(x, n)) = get_repeat_lst(x, i + n) 

get_repeat_lst_nth: LEMMA
    i < n IMPLIES
    nth(get_repeat_lst(x, n), i) = x

get_repeat_lst_mem: LEMMA
    member(y, get_repeat_lst(x, n)) IMPLIES y = x

get_repeat_lst_count: LEMMA
    IF equal?(x, y) THEN count(x, get_repeat_lst(y, n), equal?) = n
    ELSE count(x, get_repeat_lst(y, n), equal?) = 0
    ENDIF

% select_greater 
select_greater_lt: LEMMA
    member(n, lst_nat) IMPLIES n <= select_greater(lst_nat)

select_greater_plus_one_not_mem: LEMMA
    NOT member(1 + select_greater(lst_nat), lst_nat)

% matrix
matrix_append: LEMMA
    matrix?(append(lst_of_lsts1, lst_of_lsts2), n) =
    (matrix?(lst_of_lsts1, n) AND matrix?(lst_of_lsts2, n))

matrix_mem: LEMMA
   matrix?(lst_of_lsts, n) AND member(lst, lst_of_lsts) IMPLIES
   length(lst) = n

% count
count_gt0: LEMMA
   count(x, lst, equal?) > 0 IMPLIES
   EXISTS (i: below[length(lst)]): equal?(x, nth(lst, i))

count_append: LEMMA
   count(x, append(lst1, lst2), equal?) = (count(x, lst1, equal?) + count(x, lst2, equal?))
   
get_repeat_lst_count_flatten: LEMMA
    count(x, flatten(map(g)(get_repeat_lst(y, n))), equal?) =
    count(x, g(y), equal?) * n

% pareate
pareate_append: LEMMA
    (length(lst1) = length(new_lst1) AND length(lst2) = length(new_lst2)) IMPLIES
    pareate(append(lst1, lst2), append(new_lst1, new_lst2)) =
    append(pareate(lst1, new_lst1), pareate(lst2, new_lst2))

pareate_mem: LEMMA
    member(pair, pareate(lst1, lst2)) IMPLIES
    member(pair`1, lst1) AND member(pair`2, lst2)

% get_lst_until_n, get_lst_after_n
len_get_lst_until_n: LEMMA
    length(get_lst_until_n(lst, n)) = min(length(lst), n) 

nth_get_lst_until_n: LEMMA
    LET lst1 = get_lst_until_n(lst, n) IN
    i < length(lst1) IMPLIES nth(lst1, i) = nth(lst, i)

get_lst_until_n_append: LEMMA
    get_lst_until_n(append(lst1, lst2), length(lst1)) = lst1

len_get_lst_after_n: LEMMA
    length(get_lst_after_n(lst, n)) = max(length(lst) - n, 0)

nth_get_lst_after_n: LEMMA
    LET lst1 = get_lst_until_n(lst, n),
    	lst2 = get_lst_after_n(lst, n) IN
    i >= length(lst1) AND i < length(lst1) + length(lst2) IMPLIES
    nth(lst2, i - length(lst1)) = nth(lst, i) 

get_lst_after_n_append: LEMMA
    get_lst_after_n(append(lst1, lst2), length(lst1)) = lst2

% split
split_cor: LEMMA
    LET (lst1, lst2) = split(lst, n) IN
    append(lst1, lst2) = lst

split_append: LEMMA
    split(append(lst1, lst2), length(lst1)) = (lst1, lst2)

split_append_pair: LEMMA
    n = length(pair_lst`1) IMPLIES
    split(append_pair(pair_lst), n)`1 = pair_lst`1 AND
    split(append_pair(pair_lst), n)`2 = pair_lst`2 

split_matrix_len: LEMMA
    length(split_matrix(matrix, n)`1) = length(matrix)
    AND length(split_matrix(matrix, n)`2) = length(matrix)

split_matrix_nth: LEMMA
    i < length(matrix) IMPLIES
    nth(split_matrix(matrix, n)`1, i) = split(nth(matrix, i), n)`1   

split_matrix_nth2: LEMMA
    i < length(matrix) IMPLIES
    nth(split_matrix(matrix, n)`2, i) = split(nth(matrix, i), n)`2   

split_matrix_preserves_matrix: LEMMA
    i <= n AND matrix?(matrix, n) IMPLIES matrix?(split_matrix(matrix, i)`1, i)

split_matrix_preserves_matrix2: LEMMA
    i <= n AND matrix?(matrix, n) IMPLIES matrix?(split_matrix(matrix, i)`2, n-i)

% snd_proj:
snd_proj_pareate: LEMMA
    length(lst1) = length(lst2) IMPLIES 
    snd_proj(pareate(lst1, lst2)) = lst2

snd_proj_pareate2: LEMMA
    length(lst2) <= length(lst1) IMPLIES
    snd_proj(pareate(lst1, lst2)) = lst2

% sum, get_lst_mult
get_lst_mult_count_increment: LEMMA
    i < length(coef) AND i < length(lst) IMPLIES 
    IF equal?(x, nth(lst, i)) THEN 
       count(x, get_lst_mult(lst, increment(coef, i, inc)), equal?) =
       count(x, get_lst_mult(lst, coef), equal?) + inc
    ELSE count(x, get_lst_mult(lst, increment(coef, i, inc)), equal?) =
    	 count(x, get_lst_mult(lst, coef), equal?)
    ENDIF

get_lst_mult_null: LEMMA
    null?(get_lst_mult(lst, lst_pos_nat)) IMPLIES null?(lst) OR null?(lst_pos_nat)

get_lst_mult_append: LEMMA
    length(lst1) = length(coef1) IMPLIES
    get_lst_mult(append(lst1, lst2), append(coef1, coef2)) =
    append(get_lst_mult(lst1, coef1), get_lst_mult(lst2, coef2))

get_lst_mult_count_append: LEMMA
    length(lst1) = length(coef1) IMPLIES 
    count(x, get_lst_mult(append(lst1, lst2), append(coef1, coef2)), equal?) =
    count(x, get_lst_mult(lst1, coef1), equal?) + count(x, get_lst_mult(lst2, coef2), equal?)

get_lst_mult_count: LEMMA
    count(x, flatten(map(g)(get_lst_mult(lst, coef))), equal?) =
    dot_product(map(count(x, equal?))(map(g)(lst)), coef)

% remove
remove_len: LEMMA
    i < length(lst) IMPLIES
    length(remove(lst, i)) = length(lst) - 1

gt_nth_remove: LEMMA
    i < length(lst) AND i > n IMPLIES
    nth(remove(lst, i), n) = nth(lst, n) 

gt_nth_remove2: LEMMA
    i < length(lst) AND i > n IMPLIES
    nth(remove(lst, n), i-1) = nth(lst, i) 

gt_remove_remove: LEMMA
    i < length(lst) AND i > n IMPLIES
    remove(remove(lst, n), i-1) = remove(remove(lst, i), n)

remove_append: LEMMA
    remove(append(lst1, lst2), i) =
    IF i < length(lst1) THEN append(remove(lst1, i), lst2)
    ELSE append(lst1, remove(lst2, i - length(lst1)))
    ENDIF

flatten_remove: LEMMA
    i < length(lst_of_lsts) AND j < length(nth(lst_of_lsts, i)) IMPLIES
    remove(flatten(lst_of_lsts), ind_flatten(lst_of_lsts, i, j)) =
    flatten(remove(lst_of_lsts, i, j))

count_remove: LEMMA
    i < length(lst) IMPLIES
    IF equal?(x, nth(lst, i)) THEN count(x, lst, equal?) = count(x, remove(lst, i), equal?) + 1
    ELSE count(x, lst, equal?) = count(x, remove(lst, i), equal?)
    ENDIF

% remove_common, get_common
remove_common_mem: LEMMA
    member(x, remove_common(lst1, lst2, equal?)) IMPLIES member(x, lst1) 

get_common_null: LEMMA
    null?(lst2) IMPLIES
    null?(get_common(lst1, lst2, equal?))

get_common_remove_nth: LEMMA 
    n < length(lst1) AND NOT pos?(nth(lst1, n), lst2, equal?)`1 IMPLIES
    get_common(lst2, remove(lst1, n), equal?) =
    get_common(lst2, lst1, equal?)

get_common_bool: LEMMA
    LET (bool1, ind1) = pos?(x, lst1, equal?),
        (bool2, ind2) = pos?(x, lst2, equal?),  
	(bool12, ind12) = pos?(x, get_common(lst1, lst2, equal?), equal?) IN 
	(bool1 AND bool2) IFF bool12

get_common_remove: LEMMA 
    LET (bool1, ind1) = pos?(x, lst1, equal?),
        (bool2, ind2) = pos?(x, lst2, equal?),  
	(bool12, ind12) = pos?(x, get_common(lst1, lst2, equal?), equal?) IN 
	bool12 IMPLIES 
	remove(get_common(lst1, lst2, equal?), ind12) =
	get_common(remove(lst1, ind1), remove(lst2, ind2), equal?)
    
same_count?_get_lst_mul: LEMMA
    LET (new_args, new_coef) = count_elem(lst1, coef, lst2, equal?) IN
    length(lst1) = length(coef) IMPLIES
    same_count?(append(lst2, get_lst_mult(lst1, coef)), get_lst_mult(new_args, new_coef), equal?)
    
% list2set
mem_lst2set: LEMMA
   member(x, lst) IMPLIES member(x, list2set[T](lst))

mem_lst2set_2: LEMMA
   member(x, list2set(lst)) IMPLIES member(x, lst) 

is_fin_lst2set: LEMMA
   is_finite(list2set(lst_of_fin_sets))

mem_union_lst2set: LEMMA % check also mem_union_map, it's more specific
   member(x, Union(list2set(lst_of_fin_sets))) IFF
   EXISTS fin_set:
   	  member(x, fin_set) AND member(fin_set, lst_of_fin_sets)

% cardinality
card_surj_aux: LEMMA
    (EXISTS (f: [(fin_set1) -> (fin_set2)]): surjective?(f)) AND 
    (EXISTS (f: [(fin_set2) -> below[n]]): surjective?(f)) IMPLIES
    (EXISTS (f: [(fin_set1) -> below[n]]): surjective?(f))

card_surj: LEMMA
    (EXISTS (f : [(fin_set1)->(fin_set2)]) : surjective?(f)) 
		IMPLIES card(fin_set2) <= card(fin_set1)
		
END list_theory

% differs from list theory since takes two types
list_theory2 [T1, T2: TYPE]: THEORY
BEGIN
    IMPORTING list_theory[T1]

    lst, lst1: VAR list[T1]
    lst_pair, lst_pair1, lst_pair2: VAR list[[T1, T2]]
    lst_pair_: VAR list[[list[T1], T2]]
    pair: VAR [T1, T2]
    pair_: VAR [list[T1], T2]
    f: VAR [[T1, T2] -> T1]
    x, x1: VAR T1
    y: VAR T2
    n: VAR nat
    
% will be needed for a lemma
first_proj(pair): T1 = pair`1

% receives a list of the form [[x1, y1], ..., [xn, yn]]
% returns [x1, ..., xn]
first_proj(lst_pair): RECURSIVE list[T1] = 
    IF null?(lst_pair) THEN null
    ELSE LET x = car(lst_pair)`1 IN
         cons(x, first_proj(cdr(lst_pair)))
    ENDIF
MEASURE lst_pair BY << 

first_proj(lst_pair_): RECURSIVE list[list[T1]] = 
    IF null?(lst_pair_) THEN null
    ELSE cons(car(lst_pair_)`1, first_proj(cdr(lst_pair_)))
    ENDIF
MEASURE lst_pair_ BY << 

% receives a list of the form [[x1, y1], ..., [xn, yn]]
% returns [y1, ..., yn]
sec_proj(lst_pair): RECURSIVE list[T2] = 
    IF null?(lst_pair) THEN null
    ELSE LET y = car(lst_pair)`2 IN
         cons(y, sec_proj(cdr(lst_pair)))
    ENDIF
MEASURE lst_pair BY << 

first_proj_nth_mem: LEMMA
    n < length(lst_pair) IMPLIES 
    member(nth(lst_pair, n)`1, first_proj(lst_pair)) 

first_proj_is_map: LEMMA
   EXISTS f:
        FORALL lst_pair:  first_proj(lst_pair) = map(f)(lst_pair)

first_proj_mem: LEMMA
   member(x, first_proj(lst_pair)) IFF
   EXISTS pair:
      member(pair, lst_pair) AND x = pair`1

sec_proj_mem: LEMMA
   member(y, sec_proj(lst_pair)) IFF
   EXISTS pair:
      member(pair, lst_pair) AND y = pair`2


first_proj_mem_flatten: LEMMA
   member(x, flatten(first_proj(lst_pair_))) IFF 
   EXISTS pair_:
   member(pair_, lst_pair_) AND member(x, pair_`1)


END list_theory2
  

% 
map_theory [T1, T2: TYPE]: THEORY
 BEGIN
	IMPORTING list_theory[T1], list_theory[T2]

  f: VAR [T1 -> T2]
  f_set: VAR [T1 -> finite_set[T2]]
  f_lst: VAR [T1 -> list[T1]]
  f_lst2: VAR [list[T1] -> list[T2]]
  f_lst3: VAR [T1 -> list[T2]]
  
  x: VAR T2 
  y, y1: VAR T1
  fin_set: VAR finite_set[T2]

  l1, l2: VAR list[T1]
  lst_of_lsts: VAR list[list[T1]]
  mult: VAR list[nat]

map_append: LEMMA
    map(f, append(l1, l2)) = append(map(f, l1), map(f, l2))

map_append2: LEMMA
    map(f)(append(l1, l2)) = append(map(f)(l1), map(f)(l2))

map_member: LEMMA
    member(fin_set, map(f_set)(l1)) IFF 
    EXISTS y:
    member(y, l1) AND fin_set = f_set(y)

map_member2: LEMMA
    member(y, l1) IMPLIES member(f_set(y), map(f_set)(l1))

map_member3: LEMMA
    member(x, map(f)(l1)) = EXISTS (n: below[length(l1)]): x = f(nth(l1, n))

map_member4: LEMMA
    member(x, map(f)(l1)) IFF EXISTS y: member(y, l1) AND x = f(y)
    
mem_union_map: LEMMA
    member(x, Union(list2set(map(f_set)(l1)))) IFF
    EXISTS y:
        member(y, l1) AND member(x, f_set(y))

mem_flatten_map: LEMMA % the next lemma is more specific
    member(y1, flatten(map(f_lst)(l1))) IMPLIES 
    EXISTS y: 
       member(y, l1) AND member(y1, f_lst(y))

mem_flatten_map2: LEMMA
    member(x, flatten(map(f_lst3)(l1))) IFF
    EXISTS y:
       member(y, l1) AND member(x, f_lst3(y))
        
func_flatten_map: LEMMA
    (EXISTS f:
    	FORALL l1: 
        f_lst2(l1) = map(f)(l1))
    IMPLIES
    f_lst2(flatten(lst_of_lsts)) = flatten(map(f_lst2)(lst_of_lsts))

func_flatten_map2: LEMMA
    (f_lst2(null) = null AND FORALL l1, l2: f_lst2(append(l1, l2)) = append(f_lst2(l1), f_lst2(l2))) IMPLIES
    f_lst2(flatten(lst_of_lsts)) = flatten(map(f_lst2)(lst_of_lsts))

END map_theory

% this theory depends on importing "list_theory" with a specific type T
more_list_theory_props [T: TYPE]: THEORY 
BEGIN
    IMPORTING list_theory[T], list_theory[list[T]], list_theory[nat]

x: VAR T
h: VAR [list[T] -> T]

lst: VAR list[T]
lst_of_lsts: VAR list[list[T]]

n: VAR nat

% an equivalence definition
equiv: TYPE = {R: PRED[[T, T]]| equivalence?(R)}
equal?: VAR equiv

get_repeat_lst_map_count: LEMMA
    map(count(x, equal?))(get_repeat_lst(null, n)) = get_repeat_lst(0, n)

map_split: LEMMA
    split(map(h)(lst_of_lsts), n)`1 = map(h)(split[list[T]](lst_of_lsts, n)`1)

map_split2: LEMMA
    split(map(h)(lst_of_lsts), n)`2 = map(h)(split[list[T]](lst_of_lsts, n)`2)

split_no_null_entries?1: LEMMA
    no_null_entries?(lst_of_lsts) IMPLIES no_null_entries?(split(lst_of_lsts, n)`1)

split_no_null_entries?2: LEMMA
    no_null_entries?(lst_of_lsts) IMPLIES no_null_entries?(split(lst_of_lsts, n)`2)

END more_list_theory_props
