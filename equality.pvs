% Authors:
%		Gabriel Ferreira Silva (*)
% 		Mauricio Ayala-Rincón (*)
% 		Daniele Nantes Sobrinho (*)
% 		Maribel Fernandez (**)
%		(*) - University of Brasília			(**) - King's College London
%

% File: Equality.pvs
% Description: Definition of equality between term and related lemmas
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

equality: THEORY
BEGIN
	IMPORTING term_properties

t, t1, t2, t3, s: VAR term
args1, args2, args3, args4: VAR args
sym, f: VAR string

i, j, n: VAR nat
coef: VAR list[nat]


equal?(t, s): RECURSIVE bool =
    CASES t OF
        const(a): s = const(a),

	variable(X): s = variable(X), 
	
	unit: s = unit, 
	
	pair(t1, t2): pair?(s) AND equal?(t1, term1(s)) AND equal?(t2, term2(s)),

	app(sym, arg): app?(s) AND sym = f_sym(s) AND equal?(arg, arg(s)),

	ac_app(sym, arg): ac_app?(s) AND sym = ac_sym(s) AND EXISTS i, j:
	   equal?(select(sym, t)(i), select(sym, s)(j)) AND 
	   equal?(delete(sym, t)(i), delete(sym, s)(j))
    ENDCASES
MEASURE size(t) 

% test if two list of arguments are equal
equal?(args1, args2): RECURSIVE bool =
    IF null?(args1) THEN null?(args2)
    ELSIF null?(args2) THEN FALSE
    ELSE LET t = car(args1), s = car(args2) IN
    	 equal?(t, s) AND equal?(cdr(args1), cdr(args2))
    ENDIF
MEASURE args1 BY << 

% test if args1 and args2 are permutations of each other, where the notion of equality is
% given by equal?
perm?(args1, args2): RECURSIVE bool =
    IF null?(args1) THEN null?(args2)
    ELSE EXISTS (i: below[length(args1)]), (j: below[length(args2)]):
    	 equal?(nth(args1, i), nth(args2, j)) AND
	 perm?(remove(args1, i+1), remove(args2, j+1))
    ENDIF
MEASURE args1 BY << 


%%%%%%%%%%%%%%% Theorems
equal?_len: LEMMA
    equal?(args1, args2) IMPLIES length(args1) = length(args2) 

equal?_append: LEMMA
    equal?(args1, args2) AND equal?(args3, args4) IMPLIES
    equal?(append(args1, args3), append(args2, args4))

equal?_get_repeat_lst: LEMMA
    equal?(t, s) IMPLIES equal?(get_repeat_lst(t, n), get_repeat_lst(s, n))

equal?_get_lst_mult: LEMMA
    equal?(args1, args2) IMPLIES
    equal?(get_lst_mult(args1, coef), get_lst_mult(args2, coef))

equal?_ref: CONJECTURE
    equal?(t, t) 

equal?_sym: LEMMA
    equal?(t, s) IMPLIES equal?(s, t) 

equal?_ac: CONJECTURE
    are_ac?(t, s, f) AND equal?(t, s) IMPLIES
    FORALL i:
        EXISTS j: 
	   equal?(select(f, t)(i), select(f, s)(j)) AND
	   equal?(delete(f, t)(i), delete(f, s)(j))
	   
equal?_trans: LEMMA
    equal?(t1, t2) AND equal?(t2, t3) IMPLIES equal?(t1, t3) 

perm?_sym: CONJECTURE
    perm?(args1, args2) IMPLIES perm?(args2, args1) 

perm?_trans: CONJECTURE
    perm?(args1, args2) AND perm?(args2, args3) IMPLIES perm?(args1, args3)

equal?_perm: CONJECTURE
    equal?(args1, args2) IMPLIES perm?(args1, args2) 

ac_perm_args: CONJECTURE
    are_ac?(t, s, sym) AND equal?(t, s) IMPLIES perm?(get_args(sym)(t), get_args(sym)(s))
   
perm_args_ac: CONJECTURE
    perm?(get_args(sym)(t), get_args(sym)(s)) AND are_ac?(t, s, sym) IMPLIES equal?(t, s) 

perm_args_ac2: CONJECTURE
    are_ac?(t, s, f) AND equal?(t, s) IMPLIES perm?(get_args(f)(t), get_args(f)(s)) 

perm_get_ac_from_args: CONJECTURE
    perm?(args1, args2) IMPLIES 
    perm?(get_args(f)(get_ac_from_args(f)(args1)), get_args(f)(get_ac_from_args(f)(args2)))

END equality
